; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OpenClinica"
#define MyAppVersion "3.1.4"
#define MyAppPublisher "OpenClinica"
#define MyAppURL "https://www.openclinica.com//"
;#define MyAppExeName "MyProg.exe"

#define BaseDirName="{sd}\OpenClinica"
#define TmpDir="{tmp}\OC"
#define InnoDir="Z:\www\openclinica\windows\inno"

#define DateTime GetDateTimeString('yyyymmddhhnn', '', '');

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{6F25ACBE-4C00-44CF-81B1-9ED521DA42FA}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}

DefaultDirName={#BaseDirName}
DefaultGroupName={#MyAppName}
DisableDirPage=yes
DisableProgramGroupPage=yes

LicenseFile={#InnoDir}\resources\license.txt
InfoBeforeFile={#InnoDir}\resources\EULA.txt
;InfoAfterFile={#InnoDir}\resources\post_install_instructions.txt

OutputDir={#InnoDir}\output
OutputBaseFilename=OpenClinica_{#MyAppVersion}_{#DateTime}
Compression=lzma
SolidCompression=yes

SetupIconFile={#InnoDir}\resources\ocportal.ico
WizardImageFile={#InnoDir}\resources\ocportal.bmp
WizardSmallImageFile={#InnoDir}\resources\ocportal_small.bmp

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Types]
Name: full; Description: Full installation
Name: custom; Description: Custom installation; Flags: iscustom

[Components]
Name: program; Description: Program Files; Types: full custom; Flags: fixed
Name: webservices; Description: OpenClinica Web Services Package; Types: full

[Files]
Source: {#InnoDir}\source\*; DestDir: {#TmpDir}; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#InnoDir}\scripts\*; DestDir: {#TmpDir}; Flags: ignoreversion recursesubdirs createallsubdirs

Source: {#InnoDir}\extra\*; DestDir: {#TmpDir}; Components: webservices; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#InnoDir}\resources\ocportal.ico; DestDir: {app}; Flags: ignoreversion recursesubdirs createallsubdirs

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{commondesktop}\{#MyAppName}"; Filename: http://localhost:{code:GetTomcatPort}/OpenClinica; IconFilename: "{app}\ocportal.ico"
Name: "{commondesktop}\{#MyAppName} WebServices"; Filename: http://localhost:{code:GetTomcatPort}/OpenClinica-ws; IconFilename: "{app}\ocportal.ico"; Components: webservices
Name: "{commondesktop}\Start {#MyAppName} Service"; Filename: sc; Parameters: start tomcat6 ; IconFilename: "{app}\ocportal.ico"
Name: "{commondesktop}\Stop {#MyAppName} Service"; Filename: sc; Parameters: stop tomcat6 ; IconFilename: "{app}\ocportal.ico"

Name: "{group}\{#MyAppName}"; Filename: http://localhost:{code:GetTomcatPort}/OpenClinica; IconFilename: "{app}\ocportal.ico"
Name: "{group}\{#MyAppName} WebServices"; Filename: http://localhost:{code:GetTomcatPort}/OpenClinica-ws; IconFilename: "{app}\ocportal.ico"; Components: webservices
Name: "{group}\Start {#MyAppName} Service"; Filename: sc; Parameters: start tomcat6 ; IconFilename: "{app}\ocportal.ico"
Name: "{group}\Stop {#MyAppName} Service"; Filename: sc; Parameters: stop tomcat6 ; IconFilename: "{app}\ocportal.ico"
Name: "{group}\Uninstall {#MyAppName}"; Filename: "{uninstallexe}"

[Run]

; Install JRE using appropriate installer for x86 or x64 system
Filename: {#TmpDir}\jre-6u24-windows-x64.exe; Parameters: /s; StatusMsg: Installing Java JRE 1.6.0_24 x64; Check: IsWin64; Flags: runminimized
Filename: {#TmpDir}\jre-6u24-windows-i586.exe; Parameters: /s; StatusMsg: Installing Java JRE 1.6.0_24; Check: not IsWin64; Flags: runminimized

; Install PostgreSQL
Filename: {#TmpDir}\postgresql-8.4.7-2-windows.exe; Parameters:--mode unattended --install_plpgsql 0 --superpassword {code:GetPostgrePWD}; StatusMsg: Installing PostgreSQL 8.4.7.2; Flags: runminimized

; Create PostgreSQL DB role and Database
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\db_create.sql __DBUSER__ {code:GetUserName}"; Flags: runminimized;
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\db_create.sql __DBPASS__ {code:GetPassword}"; Flags: runminimized;
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\db_create.sql __DBNAME__ {code:GetDatabaseName}"; Flags: runminimized;
Filename: {#TmpDir}\db_create.cmd; Parameters: 32 postgres {code:GetPostgrePWD}; Flags: runminimized; Check: not IsWin64
Filename: {#TmpDir}\db_create.cmd; Parameters: 64 postgres {code:GetPostgrePWD}; Flags: runminimized; Check: IsWin64

; Install Apache Tomcat
Filename: {#TmpDir}\apache-tomcat-6.0.32.exe; Parameters: /S /D={app}\tomcat; StatusMsg: Installing Apache Tomcat 6.0.32; Flags: runminimized

; Replace server.xml config file for tomcat
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\server.xml __HTTP-PORT__ {code:GetTomcatPort}"; Flags: runminimized
Filename: {#TmpDir}\copy.cmd; Parameters: "{#TmpDir}\server.xml {app}\tomcat\conf\server.xml"; Flags: runminimized 

; Rename original webapps folder
Filename: {#TmpDir}\rename.cmd; Parameters: """{app}\tomcat\webapps"" oldwebapps"; Flags: runminimized

; Extract OpenClinica Package
Filename: {#TmpDir}\extract_openclinica.cmd; Parameters: """{#TmpDir}\OpenClinica-3.1.4.1.zip"" OpenClinica-3.1.4.1/distribution/OpenClinica.war ""{app}\tomcat\webapps\OpenClinica.war"""; StatusMsg: Extracting OpenClinica; Flags: runminimized
; Extract OpenClinica WebServices Package
Filename: {#TmpDir}\extract_openclinica.cmd; Parameters: """{#TmpDir}\OpenClinica-ws-3.1.4.1.zip"" OpenClinica-ws-3.1.4.1/distribution/OpenClinica-ws.war ""{app}\tomcat\webapps\OpenClinica-ws.war"""; StatusMsg: Extracting OpenClinica Web Services Package; Flags: runminimized; Components: webservices

; Import registry keys for Tomcat's Java settings
Filename: regedit.exe; Parameters: /S {#TmpDir}\reg_keys\tomcat32.reg; Check: not IsWin64; Flags: runminimized
Filename: regedit.exe; Parameters: /S {#TmpDir}\reg_keys\tomcat32_ws.reg; Check: not IsWin64;  Flags: runminimized; Components: webservices
Filename: regedit.exe; Parameters: /S {#TmpDir}\reg_keys\tomcat64.reg; Check: IsWin64; Flags: runminimized
Filename: regedit.exe; Parameters: /S {#TmpDir}\reg_keys\tomcat64_ws.reg; Check: IsWin64; Flags: runminimized; Components: webservices

; Install OpenClinica (start and stop tomcat service)
Filename: {#TmpDir}\StartService.cmd; Flags: runminimized;

; Sleep for 5 minutes
Filename: ping.exe; Parameters: {code:MySleep}; StatusMsg: Deployment of OpenClinica. It will take 5 minutes. Please wait.; Flags: runminimized

Filename: {#TmpDir}\StopService.cmd; Flags: runminimized; 

; Delete Openclinica.war
Filename: {#TmpDir}\delete.cmd; Parameters: {app}\tomcat\webapps; Flags: runminimized

; Replace user and password in datainfo.properties
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties __DBUSER__ {code:GetUserName}"; Flags: runminimized
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties __DBPASS__ {code:GetPassword}"; Flags: runminimized
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties __DBNAME__ {code:GetDatabaseName}"; Flags: runminimized

; Replace user and password and admin email in datainfo.properties for WebServices
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties_ws __DBUSER__ {code:GetUserName}"; Flags: runminimized; Components: webservices
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties_ws __DBPASS__ {code:GetPassword}"; Flags: runminimized; Components: webservices
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties_ws __DBNAME__ {code:GetDatabaseName}"; Flags: runminimized; Components: webservices
Filename: {#TmpDir}\fart.exe; Parameters: "{#TmpDir}\datainfo.properties_ws admin@example.com {code:GetAdminEmail}"; Flags: runminimized; Components: webservices

; Copy datainfo.properties
Filename: {#TmpDir}\copy.cmd; Parameters: "{#TmpDir}\datainfo.properties {app}\tomcat\webapps\OpenClinica\WEB-INF\classes\datainfo.properties"; Flags: runminimized 

; Copy datainfo.properties for webservices
Filename: {#TmpDir}\copy.cmd; Parameters: "{#TmpDir}\datainfo.properties_ws {app}\tomcat\webapps\OpenClinica-ws\WEB-INF\classes\datainfo.properties"; Flags: runminimized; Components: webservices 

; Change tomcat6 displayName and description and change startup type
Filename: {#TmpDir}\renameService.cmd; Flags: runminimized

; Start OpenClinica Service
Filename: {#TmpDir}\StartService.cmd; Flags: runminimized

[UninstallRun]
; Drop Services
Filename: net; Parameters: stop postgresql-8.4 ; Flags: runminimized
Filename: net; Parameters: stop tomcat6 ; Flags: runminimized
Filename: {sys}\sc.exe; Parameters:"delete postgresql-8.4"; Flags: runminimized
Filename: {sys}\sc.exe; Parameters:"delete tomcat6"; Flags: runminimized

; Uninstall PostgreSQL
Filename: {pf}\PostgreSQL\8.4\uninstall-postgresql.exe; Parameters: --mode unattended; StatusMsg: Uninstalling PostgreSQL 8.4.7.2; Flags: runminimized

; Uninstall Apache Tomcat
Filename: {app}\tomcat\Uninstall.exe; Parameters: /S; StatusMsg: Uninstalling Apache Tomcat 6.0.32; Flags: runminimized

[UninstallDelete]
; Delete OpenClinica directory
Type: filesandordirs; Name: {app}

[Code]
var
AuthPage,EmailPage,PostgreSQLPage : TInputQueryWizardPage;
SummaryPage : TOutputMsgWizardPage;

procedure InitializeWizard();
begin
AuthPage := CreateInputQueryPage(wpSelectComponents,
    'OpenClinica Database credentials', '','Please enter your credentials for OpenClinica Database');
  AuthPage.Add('Database name:', False);
  AuthPage.Add('Username:', False);
  AuthPage.Add('Password:', True);
  AuthPage.Add('Apache Tomcat Port:', False);
  AuthPage.Values[0] := 'openclinica';
  AuthPage.Values[1] := 'clinica';
  AuthPage.Values[2] := 'clinica';
  AuthPage.Values[3] := '8080';

 
EmailPage :=  CreateInputQueryPage(wpSelectComponents+2, 'Administrators email', '', 'Please enter administrators email for OpenClinica WebServices');
  EmailPage.Add('Administrators email:', False);
  EmailPage.Values[0] := 'admin@example.com';

PostgreSQLPage := CreateInputQueryPage(wpSelectComponents+1, 'PostgreSQL Superuser password', '', 'Please enter password for PostgreSQL superuser account');
  PostgreSQLPage.Add('PostgreSQL superuser password: (On server OS password must meet Microsoft complexity requirements', True);
  PostgreSQLPage.Values[0] := 'OpenClinica$PWD1';

SummaryPage := CreateOutputMsgPage(wpInstalling,
  'Information', 'Please read the following important information before continuing.',
  'The OpenClinica was installed with following settings: '+Chr(10)+ Chr(10)
  + 'Database name: ' + AuthPage.Values[0] + Chr(10)
  + 'Username: ' + AuthPage.Values[1] + Chr(10)
  + 'Password: ' + AuthPage.Values[2] + Chr(10)
  + 'OpenClinica URL: ' + 'http://localhost:' + AuthPage.Values[3] + '/OpenClinica' + Chr(10)
  + 'PostgreSQL superuser password: ' +  PostgreSQLPage.Values[0]
  );  
end;



function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result := False;
  if PageID = EmailPage.ID then begin
    // show email page only if webservices are selected for installation
    Result := not IsComponentSelected('webservices');
  end;
end;

function AuthForm_NextButtonClick(Page: TWizardPage): Boolean;
begin
  Result := True;
end;

function GetDatabaseName(Param: String): string;
begin
  result := AuthPage.Values[0];
end;


function GetUserName(Param: String): string;
begin
  result := AuthPage.Values[1];
end;

function GetPassword(Param: String): string;
begin
  result := AuthPage.Values[2];
end;

function GetTomcatPort(Param: String): string;
begin
  result := AuthPage.Values[3];
end;

function GetAdminEmail(Param: String): string;
begin
  result := EmailPage.Values[0];
end;

function GetPostgrePWD(Param: String): string;
begin
  result := PostgreSQLPage.Values[0];
end;

function MySleep(Param: String): string;
begin
  // Sleep 300 seconds
  Sleep(300000);                    
  result := '';
end;
  